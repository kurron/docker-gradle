apply plugin: 'groovy'

repositories {
    maven {
        name = 'kurron'
        url = 'http://dl.bintray.com/kurron/maven'
    }
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}:indy"
    compile "org.kurron:tools-java:${toolsJavaVersion}"
}

compileGroovy {
    // TODO: provide override
    groovyOptions.configurationScript = file( "/opt/gradle/gradle/groovy/groovyCompilerConfiguration.groovy" )
}

compileGroovy.groovyOptions.optimizationOptions['indy'] = true
compileTestGroovy.groovyOptions.optimizationOptions['indy'] = true

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

compileGroovy.options*.compilerArgs = ['-Xlint:deprecation','-encoding', 'UTF-8']
compileTestGroovy.options*.compilerArgs = ['-Xlint:deprecation','-encoding', 'UTF-8']

test {
    maxParallelForks = Runtime.runtime.availableProcessors()
    scanForTestClasses = false
    beforeTest { descriptor ->
        logger.lifecycle( "Running test: ${descriptor}" )
    }
    useJUnit {
        includeCategories 'org.kurron.categories.UnitTest'
    }
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    reports.html.destination =  "$buildDir/reports/unitTests"
}

task componentTest( type: Test ) {
    dependsOn = [classes, testClasses] 
    group = 'Verification'
    description = 'Runs the component-level tests.'
    maxParallelForks = Runtime.runtime.availableProcessors()
    scanForTestClasses = false
    beforeTest { descriptor ->
        logger.lifecycle( "Running test: ${descriptor}" )
    }
    useJUnit {
        includeCategories 'org.kurron.categories.ComponentTest'
    }
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    reports.html.destination =  "$buildDir/reports/componentTests"
}
 
// force Spock usage
apply from: "/opt/gradle/gradle/testing/spock.gradle"
